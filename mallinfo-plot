#!/usr/bin/gnuplot -c

if (ARGC < 1) {
    print "Usage: ./mallinfo-plot <memory-report-file> [<plot-name>]"
    exit
}

report = ARG1
desired_plot = ARGC >= 2 ? ARG2 : "malloc-used"

stats report using (timecolumn(1,"%s")):(column(2) - 1e8) name "RSS" nooutput
rss_trend(t) = RSS_intercept + t * RSS_slope

stats report using (timecolumn(1,"%s")):(column(4) + column(7)) name "MMAP_ARENAS" nooutput
mmap_arenas_trend(t) = MMAP_ARENAS_intercept + t * MMAP_ARENAS_slope

set xdata time
set format x "%H:%M"
set format y "%.0s %cB"
set xlabel "Time (HH:MM)"
set ylabel "Memory usage"
set grid
bind "q" 'exit gnuplot'
bind "Close" 'exit gnuplot'

shown_time(_) = timecolumn(1, "%s") - RSS_min_x

if (desired_plot eq "rss") {
    valid_plot = 1
    plot report using (shown_time(1)):2 title "RSS" with linespoints
    show variables all
}
if (desired_plot eq "arena") {
    valid_plot = 1
    plot report using (shown_time(1)):4 title "Arenas size" with linespoints
}
if (desired_plot eq "malloc-used") {
    valid_plot = 1
    plot \
        report using (shown_time(1)):(column(8)) title "Used (only in malloc's arena)" with line, \
        report using (shown_time(1)):(column(7)) title "Used (only in malloc's mmap)" with line, \
        report using (shown_time(1)):(column(8)+column(7)) title "Used (malloc's arena + malloc's mmap)" with line
        #report using (shown_time(1)):(column(8)+column(9)) title "Used + Free" with points
}
if (desired_plot eq "rss-anon-vs-all") {
    valid_plot = 1
    plot \
        report using (shown_time(1)):3 title "RSS (anonymous)" with linespoints, \
        report using (shown_time(1)):(column(2)-column(3)) title "RSS (anonymous + file + shared)" with linespoints
}
if (desired_plot eq "rss-anon-vs-malloc") {
    valid_plot = 1
    plot \
        report using (shown_time(1)):3 title "RSS (anonymous)" with linespoints, \
        report using (shown_time(1)):(column(4)+column(7)) title "malloc memory size (arenas + mmap)"
}
if (desired_plot eq "allocations") {
    valid_plot = 1
    set ylabel "Allocations (arena)"
    set y2label "Allocations (mmap)"
    set format y
    set ytics nomirror
    set y2tics nomirror
    set yrange [0:]
    set y2range [0:]
    plot \
        report using (shown_time(1)):3 title "# of live allocations (only in arena)" with linespoints, \
        report using (shown_time(1)):6 title "# of live allocations (only in malloc's mmap)" with linespoints axes x1y2
}

if (!exists("valid_plot")) {
    print "Unknown plot: " . desired_plot
    exit
}

# Keep running so that gnuplot does not exit and continues to redraw the plot if the window is resized
while (1) {
    pause 100
}

# vim: set ft=gnuplot autoindent:
